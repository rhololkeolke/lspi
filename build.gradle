apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'checkstyle'

sourceCompatibility = 1.8

ext.revision = 'git rev-list --count HEAD'.execute().text.trim()
ext.hash = 'git rev-parse --short HEAD'.execute().text.trim()
version = "r${revision}.${hash}"

checkstyle {
    toolVersion = "6.4.1"
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'LSPI',
                'Implementation-Version': version
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'LSPI With Dependencies',
                'Implementation-Version': version
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    maven {
        url "https://repo1.maven.org/maven2"
    }
}

dependencies {
    compile group: 'commons-cli', name: 'commons-cli', version: '1.2'
    compile group: 'com.googlecode.matrix-toolkits-java', name: 'mtj', version: '1.0.4'
    compile group: "org.slf4j", name: "slf4j-api", version: '1.7.16'
    compile group: "org.slf4j", name: "log4j-over-slf4j", version: '1.7.16'
    compile group: "ch.qos.logback", name: "logback-classic", version: '1.1.5'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}